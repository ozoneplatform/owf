buildscript {
    ext {
        grailsVersion = '3.3.2'
        gormVersion = '6.1.8.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.grails.org/grails/core' }
        jcenter()
    }
    dependencies {
        classpath group: 'io.spring.gradle', name: 'dependency-management-plugin', version: '1.0.4.RELEASE'

        classpath group: 'org.grails', name: 'grails-gradle-plugin', version: grailsVersion

        classpath group: 'org.grails.plugins', name: 'hibernate5', version: gormVersion - ".RELEASE"
        classpath group: 'org.grails.plugins', name: 'database-migration', version: '3.0.3'
    }
}


group 'org.ozoneplatform'
version '7.17.2-0-RC1'

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'groovy'

apply plugin: 'war'

apply from: 'gradle/build-javascript-resources.gradle'
apply from: 'gradle/build-theme-resources.gradle'

apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.grails-gsp'


ext {
    releaseVersion = version.toString().replaceFirst("-", ".")

    tomcatBundleStaging = "$buildDir/staging/bundle"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.grails.org/grails/core' }
    maven { url 'http://repository.springsource.com/maven/bundles/release' }
    maven { url 'http://repository.springsource.com/maven/bundles/external' }
    maven { url 'https://packages.atlassian.com/maven-3rdparty-legacy' }
}

dependencyManagement {
    imports {
        mavenBom 'org.grails:grails-bom:' + grailsVersion
        mavenBom 'org.ozoneplatform:ozone-classic-bom:7.17.2-0-SNAPSHOT'
    }

    applyMavenExclusions false
}

grails {
    plugins {
        compile project(':owf-example-widgets')
    }
}

configurations {
    customTomcat {}
    drivers {}
}

dependencies {
    // Ozone
    compile group: 'org.ozoneplatform', name: 'owf-appconfig', version: '0.9.1-0-RC1'
    compile group: 'org.ozoneplatform', name: 'owf-auditing', version: '1.3.2-0-RC1'
    compile group: 'org.ozoneplatform', name: 'owf-security', version: '4.0.4-0-RC1'
    compile group: 'org.ozoneplatform', name: 'owf-messaging', version: '1.19.1-0-RC1'

    customTomcat(group: 'org.ozoneplatform', name: 'owf-custom-tomcat', version: '1.2.3-0-RC1') {
        artifact {
            name = 'owf-custom-tomcat'
            type = 'zip'
        }
    }

    // Spring Boot
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging'

    provided group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'

    // Grails
    compile group: 'org.grails', name: 'grails-core'
    compile group: 'org.grails', name: 'grails-web-boot'
    compile group: 'org.grails', name: 'grails-logging'

    compile group: 'org.grails', name: 'grails-plugin-rest'
    compile group: 'org.grails', name: 'grails-plugin-databinding'
    compile group: 'org.grails', name: 'grails-plugin-i18n'
    compile group: 'org.grails', name: 'grails-plugin-services'
    compile group: 'org.grails', name: 'grails-plugin-url-mappings'
    compile group: 'org.grails', name: 'grails-plugin-interceptors'

    compile group: 'org.grails.plugins', name: 'cache'
    compile group: 'org.grails.plugins', name: 'async'
    compile group: 'org.grails.plugins', name: 'scaffolding'
    compile group: 'org.grails.plugins', name: 'events'
    compile group: 'org.grails.plugins', name: 'hibernate5'
    compile group: 'org.grails.plugins', name: 'gsp'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.10'

    console group: 'org.grails', name: 'grails-console'

    profile group: 'org.grails.profiles', name: 'web'

    runtime group: 'org.glassfish.web', name: 'el-impl', version: '2.1.2-b03'
    runtime group: 'com.h2database', name: 'h2'
    runtime group: 'org.apache.tomcat', name: 'tomcat-jdbc'

    drivers group: 'org.postgresql', name: 'postgresql'

    testCompile group: 'org.grails', name: 'grails-gorm-testing-support'
    testCompile group: 'org.grails', name: 'grails-web-testing-support'
    testCompile group: 'org.grails', name: 'grails-datastore-rest-client'

    testRuntime group: 'cglib', name: 'cglib-nodep'

    // Grails Plugins
    compile group: 'org.grails.plugins', name: 'converters'
    compile group: 'org.grails.plugins', name: 'quartz'
    compile group: 'org.grails.plugins', name: 'grails-pretty-time'

    // Other
    compile group: 'com.google.code.findbugs', name: 'jsr305'

    compile group: 'org.hibernate', name: 'hibernate-core'

    compile group: 'org.apache.httpcomponents', name: 'httpcore'
    compile group: 'org.apache.httpcomponents', name: 'httpclient'

    compile group: 'commons-fileupload', name: 'commons-fileupload'

    // Ruby Gems (to build themes)
    themeCompileGem group: 'rubygems', name: 'sass', version: '3.1.3'
    themeCompileGem group: 'rubygems', name: 'compass', version: '0.11.7'
}


bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always',
            '-Duser=testAdmin1',
            '-Dowf.db.init',
            '-Ddisable.auto.recompile=false',
            '-Xverify:none')

    addResources = false
}

war {
    // Exclude the Spring .xml configuration files from the .war
    // They will be copied to the Tomcat classpath at /tomcat/libs
    rootSpec.exclude('ozone/framework/**')
}

idea {
    module {
        excludeDirs += file('archive')
        excludeDirs += file('src/main/resources/public')
    }
}

task extractCustomTomcat(type: Sync, group: 'bundle') {
    dependsOn(configurations.customTomcat)

    from { // use of closure defers evaluation until execution time
        configurations.customTomcat.collect { zipTree(it) }
    }
    into "$tomcatBundleStaging"
}

task copyDatabaseDrivers(type: Copy, group: 'bundle') {
    dependsOn(extractCustomTomcat)

    from configurations.drivers
    into file("$tomcatBundleStaging/drivers")
}

task copyFrameworkConfigurations(type: Copy, group: 'bundle') {
    dependsOn(extractCustomTomcat)

    from fileTree("config")
    into file("$tomcatBundleStaging/tomcat/lib/config")
}

task copyFrameworkSpringConfigurations(type: Copy, group: 'bundle') {
    dependsOn(extractCustomTomcat)

    from fileTree("src/main/resources/ozone/framework")

    into file("$tomcatBundleStaging/tomcat/lib/ozone/framework")
}

task copyFrameworkWar(type: Copy, group: 'bundle') {
    dependsOn(extractCustomTomcat, ':war')

    from tasks.getByPath(':war')
    rename("owf-framework.*", 'owf.war')
    into file("$tomcatBundleStaging/tomcat/webapps")
}

task bundle(type: Zip, group: 'build') {
    dependsOn(copyFrameworkConfigurations,
              copyFrameworkSpringConfigurations,
              copyDatabaseDrivers,
              copyFrameworkWar)

    from file(tomcatBundleStaging)

    archiveName "ozone-framework-${releaseVersion}.zip"

    destinationDir buildDir
}


apply from: 'gradle/report-test-coverage.gradle'


task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
